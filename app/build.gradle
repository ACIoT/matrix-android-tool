import com.android.builder.core.BuilderConstants

apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties
if (keystorePropertiesFile.exists()) {
    keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def getGitVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "ablecloud.matrix.tool"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 211
        versionName "2.1.1"
        setProperty("archivesBaseName", "${project.getParent().getName()}-${getGitVersionName}")

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "MAJOR_DOMAIN", '"ablecloud"'
        buildConfigField "long", "MAJOR_DOMAIN_ID", "3L"
    }

    dataBinding {
        enabled true
    }

    signingConfigs {
        release {
            if (keystoreProperties != null) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == BuilderConstants.RELEASE) {
            def assembleTask = variant.assemble
            assembleTask.doLast {
                def name = project.getParent().getName()
                copy {
                    from(variant.outputs[0].outputFile) {
                        rename "${name}(.*)release.apk", "${name}.apk"
                    }
                    into '../'
                }
                println "Publish output file of task '${assembleTask.name}'"
            }
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile(name: 'ablecloud-matrix-app-sdk-v2.1.0-2-ga81e42a-release', ext: 'aar')
    compile(name: 'ablecloud-matrix-local-sdk-v2.1.0-2-ga81e42a-release', ext: 'aar')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'

    compile 'io.reactivex.rxjava2:rxjava:2.0.7'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.okhttp3:okhttp:3.7.0'

    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    compile 'com.sloydev:preferator:1.1.0'
    compile 'com.github.ganfra:material-spinner:2.0.0'
}
